buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.anarres.jarjar:jarjar-gradle:1.0.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'org.anarres.jarjar'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "io.github.xkerman.helloworld"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.appcompat:appcompat-resources:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation jarjar.repackage {
        // Android SDK version < 26 does not support MethodHandle.invoke() / MethodHandle.invokeExact()
        // see: https://developer.android.com/studio/write/java8-support
        // Since websocket-client version >= 9.4.20 contains MethodHandle.invoke(),
        // we need to remove websocket-client here and use websocket-client version <= 9.4.19
        from ('net.sourceforge.htmlunit:htmlunit:2.36.0') {
            exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
        }
        classRename 'org.apache.http.**', 'my.org.apache.http.@1'
        classRename 'org.w3c.dom.**', 'my.org.w3c.dom.@1'
    }
    implementation 'org.eclipse.jetty.websocket:websocket-client:9.4.19.v20190610'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}